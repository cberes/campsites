buildscript {
	ext {
		springBootVersion = '2.0.0.RELEASE'
	}
	repositories {
		mavenCentral()
		maven { url 'https://repo.spring.io/snapshot' }
		maven { url 'https://repo.spring.io/milestone' }
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath 'org.junit.platform:junit-platform-gradle-plugin:1.1.0'
	}
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.junit.platform.gradle.plugin'

ext.moduleName = 'net.seabears.campsites.api'
mainClassName = 'net.seabears.campsites.api.CampsitesApplication'

dependencies {
	compile project(':core')
	compile project(':database')
	compile project(':backend')
	compile "org.springframework.boot:spring-boot-starter:$springBootVersion"
	compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion") {
		exclude module: 'tomcat-annotations-api'// conflicts with Hibernate
	}
	compile "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
	runtime 'org.postgresql:postgresql:42.1.4'
	runtime 'javax.xml.bind:jaxb-api:2.3.0'
	testCompile project(':test-utils')
	testCompile("org.springframework.boot:spring-boot-starter-test:$springBootVersion") {
		exclude module: 'junit'					// using junit 5
	}
	testCompile "org.junit.jupiter:junit-jupiter-api:$junitVersion"
	testCompile "org.junit.jupiter:junit-jupiter-params:$junitVersion"
	testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
	testRuntime 'com.h2database:h2:1.4.196'
}

junitPlatform {
	filters {
		includeClassNamePattern project.hasProperty('IT') ? '^.*IT$' : '^.*Tests?$'
	}
}
