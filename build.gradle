allprojects {
	group = 'net.seabears.campsites'
	version = '0.0.1-SNAPSHOT'
}

def javaProjects = [
		project(':test-utils'),
		project(':core'),
		project(':database'),
		project(':backend'),
		project(':api')
]

configure(javaProjects) {
	apply plugin: 'java'
	apply plugin: 'checkstyle'

	ext.springBootVersion = '2.0.0.RELEASE'
	ext.springDataVersion = '2.0.5.RELEASE'
	ext.springVersion = '5.0.4.RELEASE'
	ext.junitVersion = '5.1.0'

	sourceCompatibility = 9
	targetCompatibility = 9

	afterEvaluate {
		repositories {
			mavenCentral()
			maven { url 'https://repo.spring.io/snapshot' }
			maven { url 'https://repo.spring.io/milestone' }
		}

		jar {
			inputs.property("moduleName", moduleName)
			manifest {
				attributes('Automatic-Module-Name': moduleName)
			}
		}

		checkstyle {
			checkstyleMain.exclude '**/module-info.java'
			configDir = file("$rootDir/config/checkstyle")
			toolVersion = '8.2'
			maxErrors = 0
			maxWarnings = 0
		}
	}
}
